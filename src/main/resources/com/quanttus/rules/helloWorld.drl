import com.quanttus.rules.*;
import java.util.*;

global Notifier notifier;

rule "Calories Burned"
when
    now: Now()
    user: User(validToday == false)
    dataPoint: DailyDataPoint(date == now.date,
                              user == user,
                              hoursOfData >= 14)
then
    notifier.message(user, "CaloriesBurned");
    modify(user) { setValidToday(true) };
end

rule "Calorie Goal Male"
when
    now: Now()
    user: User(hitCalorieGoal == false,
               validToday == true,
               isMale() == true)
    dataPoint: DailyDataPoint(
        date == now.date,
        user == user,
        hoursOfData >= 14,
        calories >= 13.75 * user.weight + 5.0 * user.height - 6.76 * user.age + 660)
then
    modify(user) { setHitCalorieGoal(true) };
    notifier.message(user, "CalorieGoal");
end

rule "Calorie Goal Female"
when
    now: Now()
    user: User(hitCalorieGoal == false,
               validToday == true,
               isMale() == false)
    dataPoint: DailyDataPoint(
        date == now.date,
        user == user,
        hoursOfData >= 14,
        calories >= 9.56 * user.weight + 1.85 * user.height - 4.68 * user.age + 655)
then
    modify(user) { setHitCalorieGoal(true) };
    notifier.message(user, "CalorieGoal");
end

rule "Local Max Cal"
when
    now: Now()
    $user: User(hitCalorieGoal == true)
    $today: DailyDataPoint(
        date == now.date,
        user == user,
        hoursOfData >= 14)

    accumulate(DailyDataPoint(user == user,
                              date > now.date - 7,
                              date <= now.date,
                              hoursOfData >= 14);
               $countAll : count(1))

    accumulate(DailyDataPoint(user == user,
                              date > now.date - 7,
                              date <= now.date,
                              hoursOfData >= Math.min(16, $today.hoursOfData));
               $countValid : count(1);
               $countValid >= $countAll / 2)

    accumulate(DailyDataPoint(user == user,
                              date > now.date - 7,
                              date <= now.date,
                              $calories: calories);
               $max : max($calories),
               $countDataPoints : count(1);
               $countDataPoints >= 5,
               $max < $today.calories)

then
    notifier.message($user, "LocalMax");
end
